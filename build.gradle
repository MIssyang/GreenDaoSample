// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "config.gradle"
import de.greenrobot.daogenerator.Entity
import de.greenrobot.daogenerator.Schema
import de.greenrobot.daogenerator.DaoGenerator

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-alpha4'
        //greenDao generator
        classpath 'de.greenrobot:greendao-generator:2.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//greenDao Generate Task
task daoGenerate << {
    def pack = 'cn.chenzhongjin.greendao.sample'
    Schema schema = new Schema(3, "${pack}.entity");//foucuse version for update database
    schema.defaultJavaPackageDao = "${pack}.database"
    schema.hasKeepSectionsByDefault = true
    schema.useActiveEntitiesByDefault = true;
    Entity user = schema.addEntity("User");
    user.addIdProperty();
    user.addStringProperty("name");
    user.addStringProperty("sex");
    user.addLongProperty("phoneNumber");
    user.addLongProperty("updateTime");

    new DaoGenerator().generateAll(schema, "app/src/main/java", null, null);
}