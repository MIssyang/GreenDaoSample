apply from: 'buildsystem/dependencies.gradle'

import de.greenrobot.daogenerator.Entity
import de.greenrobot.daogenerator.Schema
import de.greenrobot.daogenerator.DaoGenerator

buildscript {
    apply from: 'buildsystem/dependencies.gradle'
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        //greenDao generator
        classpath libraries.greandaoGeneratorEncryption
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//greenDao Generate Task
task daoGenerate << {
    def pack = 'cn.chenzhongjin.greendao.sample'
    Schema schema = new Schema(4, "${pack}.entity");//foucuse version for update database
    schema.defaultJavaPackageDao = "${pack}.database"
    schema.enableKeepSectionsByDefault();
    schema.enableActiveEntitiesByDefault();
    Entity user = schema.addEntity("User");
    user.addIdProperty();
    user.addStringProperty("name");
    user.addStringProperty("sex");
    user.addLongProperty("phoneNumber");
    user.addLongProperty("updateTime");
    user.addStringProperty("testUpgrade")

    new DaoGenerator().generateAll(schema, "app/src/main/java", null, null);
}